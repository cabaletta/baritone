/*
 * This file is part of Baritone.
 *
 * Baritone is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Baritone is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with Baritone.  If not, see <https://www.gnu.org/licenses/>.
 */

buildscript {
    dependencies {
        classpath "org.ow2.asm:asm:9.3"
    }
}

plugins {
    id "architectury-plugin" version "3.4.135"
    id 'dev.architectury.loom' version '0.12.0.282' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: "maven-publish"
    apply plugin: "dev.architectury.loom"

    archivesBaseName = rootProject.archives_base_name
    def vers = 'git describe --always --tags --dirty'.execute().text.trim();
    if (!vers.startsWith("v")) {
        println "git doesn't appear to be installed!"
        println "using version number: " + rootProject.mod_version
        version = rootProject.mod_version
    } else {
        version = vers.substring(1)
    }
    group = rootProject.maven_group

    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings loom.officialMojangMappings()
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:1.17.1+build.32:v2"
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"

        def targetVersion = 17
        if (JavaVersion.current().isJava9Compatible()) {
            it.options.release = targetVersion
        }
    }
}

allprojects {
    apply plugin: "architectury-plugin"

    repositories {
        maven {
            name = 'spongepowered-repo'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name = 'impactdevelopment-repo'
            url = 'https://impactdevelopment.github.io/maven/'
        }
        maven {
            name = "ldtteam"
            url = "https://maven.parchmentmc.net/"
        }
        // for the newer version of launchwrapper
        maven {
            name = "multimc-maven"
            url = "https://files.multimc.org/maven/"
            metadataSources {
                artifact()
            }
        }
        mavenCentral()
    }
}