/*
 * This file is part of Baritone.
 *
 * Baritone is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Baritone is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with Baritone.  If not, see <https://www.gnu.org/licenses/>.
 */
buildscript {
    repositories {
        mavenCentral()
        maven { url "https://maven.architectury.dev/" }
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'impactdevelopment-repo'
            url = 'https://impactdevelopment.github.io/maven/'
        }
        maven {
            url = 'https://www.dogforce-games.com/maven/'
        }
        maven {
            url = 'https://maven.fabricmc.net/'
        }
        maven {
            url = 'https://libraries.minecraft.net/'
        }
    }
    dependencies {
        classpath group: 'me.shedaniel', name: 'forgified-fabric-loom', version: '0.7.0-SNAPSHOT'
    }
}

group 'baritone'
version '1.6.3'


import baritone.gradle.task.CreateDistTask
import baritone.gradle.task.ProguardTask

def compileType = project.hasProperty("baritone.fabric_build") ? "FABRIC" : project.hasProperty("baritone.forge_build") ? "FORGE" : "OFFICIAL"

apply plugin: 'java'
apply plugin: 'forgified-fabric-loom'

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
    options.encoding = "UTF-8" // allow emoji in comments :^)
}

sourceSets {
    api {
        compileClasspath += main.compileClasspath
    }
    main {
        compileClasspath += api.output
    }
    test {
        compileClasspath += main.compileClasspath + main.runtimeClasspath + main.output
        runtimeClasspath += main.compileClasspath + main.runtimeClasspath + main.output
    }
    launch {
        compileClasspath += main.compileClasspath + main.runtimeClasspath + main.output
        runtimeClasspath += main.compileClasspath + main.runtimeClasspath + main.output
    }

    schematica_api {
        compileClasspath += main.compileClasspath
    }

    main {
        compileClasspath += schematica_api.output
    }
}

loom {
    mixinConfig 'mixins.baritone.json'
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.api.allSource
}

minecraft.refmapName = "mixins.baritone.refmap.json"

repositories {
    mavenCentral()

    maven {
        name = 'SpongePowered'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
    maven {
        name = 'impactdevelopment-repo'
        url = 'https://impactdevelopment.github.io/maven/'
    }
    maven {
        url = 'https://www.dogforce-games.com/maven/'
    }
}

dependencies {
    if (compileType.equals("FORGE")) {
        forge "net.minecraftforge:forge:1.16.5-36.1.0"
    }
    minecraft "com.mojang:minecraft:1.16.5"
    mappings "de.oceanlabs.mcp:mcp_snapshot:20201028-1.16.3"
    if (!compileType.equals("FORGE")) {
        modImplementation "net.fabricmc:fabric-loader:0.9.1+build.205"
    }
    // this makes it compile with the forge tweak stuff
    implementation 'com.github.ImpactDevelopment:SimpleTweaker:1.2'
    implementation('net.minecraft:launchwrapper:1.12') {
        exclude module: 'lwjgl'
    }

    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    testImplementation 'junit:junit:4.12'
}

javadoc {
    options.addStringOption('Xwerror', '-quiet') // makes the build fail on travis when there is a javadoc error
    options.linkSource true
    options.encoding "UTF-8" // allow emoji in comments :^)
    source = sourceSets.api.allJava
    classpath += sourceSets.api.compileClasspath
}

// skidded from fabric-example-mod (comments and all)
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"

    // The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
    // JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
    // We'll use that if it's available, but otherwise we'll use the older option.
    def targetVersion = 8
    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = targetVersion
    }
}


jar {
    from sourceSets.launch.output, sourceSets.api.output

    if (!getProject().hasProperty("baritone.forge_build")) {
        exclude "**/BaritoneForgeModXD.class"
        exclude "**/mods.toml"
    }

    preserveFileTimestamps = false
    reproducibleFileOrder = true

    if (getProject().hasProperty("baritone.fabric_build")) {
        filesMatching("fabric.mod.json") {
            expand "version": version
        }
    } else {
        exclude("fabric.mod.json")
    }


    manifest {
        attributes(
                'MixinConfigs': 'mixins.baritone.json',
                "MixinConnector": "baritone.launch.BaritoneMixinConnector",

                'Implementation-Title': 'Baritone',
                'Implementation-Version': version,
        )
    }
}

if (compileType.equals("OFFICIAL")) {
    remapJar {
        toM.set "official"
    }
}



task proguard(type: ProguardTask) {
    url 'https://downloads.sourceforge.net/project/proguard/v7.1.0-beta5/proguard-7.1.0-beta5.zip'
    extract 'proguard-7.1.0-beta5/lib/proguard.jar'
    compType compileType
}

task createDist(type: CreateDistTask, dependsOn: proguard)


build.finalizedBy(createDist)